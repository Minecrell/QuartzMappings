plugins {
    id 'com.github.hierynomus.license' version '0.11.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'license'
    apply plugin: 'maven-publish'

    group = 'net.minecrell.quartz'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:all', '-Xlint:-deprecation']
        options.encoding = 'UTF-8'
    }

    // License header formatting
    license {
        header rootProject.file('LICENSE')
        exclude '*.txt'
        exclude '*.xml'
        exclude '*.json'
        exclude 'META-INF/**'
        ignoreFailures false
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar

                if (project == rootProject) {
                    artifactId = 'mappings'
                } else {
                    artifactId = "mappings-$project.name"
                }

                repositories {
                    maven {
                        url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                    }
                }
            }
        }
    }
}

subprojects {
    dependencies {
        compile rootProject
    }
}

dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    runtime 'org.ow2.asm:asm-all:5.0.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
